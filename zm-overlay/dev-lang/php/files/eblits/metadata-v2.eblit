# Copyright 1999-2010 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: $

eblit-php-metadata() {

	php_get_uri() {
		case "${1}" in
			"php-pre")
				echo "http://downloads.php.net/johannes/${2}"
				;;
			"php")
				echo "http://www.php.net/distributions/${2}"
				;;
			"suhosin")
				echo "http://download.suhosin.org/${2}"
				;;
			"fpm")
				echo "http://launchpad.net/php-fpm/master/${2}"
				;;
			"gentoo")
				# @TODO@ remove in final version
				#echo "http://files.hoffie.info/static/${2}"
				#echo "http://hg.hoffie.info/gentoo-php-rewrite/raw-file/tip/files/${2}"
				echo "http://github.com/GiDiS/gentoo-php-rewrite/raw/master/files/${2}"
				;;
			*)
				die "unhandled case in php_get_uri"
				;;
		esac
	}

	PHP_MV="$(get_major_version)"

	# handle different types of releases (finals, rcs, alphas, betas, ...)
	PHP_PV="${PV}"
	PHP_RELEASE="php"
	if [[ ${PV} == *_rc* ]] ; then
		PHP_PV="${PV/_rc/RC}"
		PHP_EXTRA_BRANDING="${PV/*_rc/RC}"
		PHP_RELEASE="php-pre"
	fi
	if [[ ${PV} == *_alpha* ]] ; then
		PHP_PV="${PV/_alpha/alpha}"
		PHP_EXTRA_BRANDING="${PV/*_alpha/alpha}"
		PHP_RELEASE="php-pre"
	fi
	if [[ ${PV} == *_beta* ]] ; then
		PHP_PV="${PV/_beta/beta}"
		PHP_EXTRA_BRANDING="${PV/*_beta/beta}"
		PHP_RELEASE="php-pre"
	fi
	PHP_P="${PN}-${PHP_PV}"
	local PHP_SRC_URI="$(php_get_uri "${PHP_RELEASE}" "${PHP_P}.tar.bz2")"

	PHP_PATCHSET="${PHP_PATCHSET:-${PR/r/}}"
	local PHP_PATCHSET_URI="
		$(php_get_uri gentoo "php-patchset-${PV}-r${PHP_PATCHSET}.tar.bz2")"

	if [[ ${SUHOSIN_VERSION} == *-gentoo ]] ; then
		# in some cases we use our own suhosin patch (very recent version,
		# patch conflicts, etc.)
		local SUHOSIN_TYPE="gentoo"
	else
		local SUHOSIN_TYPE="suhosin"
	fi

	local SUHOSIN_URI="$(php_get_uri ${SUHOSIN_TYPE} \
		suhosin-patch-${SUHOSIN_VERSION}.patch.gz )"
	export SUHOSIN_PATCH="suhosin-patch-${SUHOSIN_VERSION}.patch"

	local FPM_PV="${FPM_VERSION}"
	export FPM_PATCH="php-fpm-${FPM_PV}~${PHP_PV:0:3}.patch"
	local FPM_URI="$(php_get_uri fpm "${FPM_PV}/+download/${FPM_PATCH}")"

	SRC_URI="
		${PHP_SRC_URI}
		${PHP_PATCHSET_URI}
		suhosin? ( ${SUHOSIN_URI} )
		fpm? ( ${FPM_URI} )"

	DESCRIPTION="The PHP language runtime engine: CLI, CGI, FPM Fast-CGI,  Apache2 and embed SAPIs."
	HOMEPAGE="http://php.net/"
	LICENSE="PHP-3"

	
	# List avaible SAPI, sorted by compiling order
	export PHP_SAPI="cli cgi fpm embed apache2"

	# Gentoo-specific, common features
	IUSE="kolab"

	# SAPIs and SAPI-specific USE flags:
	IUSE="${IUSE}
		${PHP_SAPI}
		concurrentmodphp threads
		"

	# php extensions / features
	IUSE="${IUSE} adabas bcmath berkdb birdstep bzip2 calendar cdb cjk
		crypt ctype curl curlwrappers db2 dbmaker debug doc empress
		empress-bcs enchant esoob exif frontbase fileinfo filter firebird
		flatfile ftp gd gd-external gdbm gmp hash iconv imap inifile
		interbase intl iodbc ipv6 json kerberos ldap ldap-sasl libedit
		mcve mssql mysql mysqlnd mysqli nls oci8
		oci8-instant-client odbc pcntl pdo phar pic posix postgres qdbm
		readline recode sapdb session sharedext sharedmem
		simplexml snmp soap sockets solid spell sqlite sqlite3 ssl suhosin
		sybase-ct sysvipc tidy tokenizer truetype unicode wddx
		xml xmlreader xmlwriter xmlrpc xpm xsl yaz zip zlib"

		
	# Legacy flags for bug #298205 and other
	# @TODO remove it after rewrite other ebuilds
	export LEGACY_USE_FLAGS="pcre reflection spl"
	IUSE="${IUSE} ${LEGACY_USE_FLAGS}"


	DEPEND="app-admin/php-toolkit
		>=dev-libs/libpcre-7.9[unicode]
		adabas? ( >=dev-db/unixODBC-1.8.13 )
		apache2? ( !threads? ( www-servers/apache[-threads] ) )
		berkdb? ( =sys-libs/db-4* )
		birdstep? ( >=dev-db/unixODBC-1.8.13 )
		bzip2? ( app-arch/bzip2 )
		cdb? ( || ( dev-db/cdb dev-db/tinycdb ) )
		cjk? ( !gd? ( !gd-external? (
			>=media-libs/jpeg-6b
			media-libs/libpng
			sys-libs/zlib
		) ) )
		crypt? ( >=dev-libs/libmcrypt-2.4 )
		curl? ( >=net-misc/curl-7.10.5 )
		db2? ( >=dev-db/unixODBC-1.8.13 )
		dbmaker? ( >=dev-db/unixODBC-1.8.13 )
		empress? ( >=dev-db/unixODBC-1.8.13 )
		empress-bcs? ( >=dev-db/unixODBC-1.8.13 )
		enchant? ( app-text/enchant )
		esoob? ( >=dev-db/unixODBC-1.8.13 )
		exif? ( !gd? ( !gd-external? (
			>=media-libs/jpeg-6b
			media-libs/libpng
			sys-libs/zlib
		) ) )
		firebird? ( dev-db/firebird )
		fpm? ( >=dev-libs/libevent-1.4.12 )
		gd? ( >=media-libs/jpeg-6b media-libs/libpng sys-libs/zlib )
		gd-external? ( media-libs/gd )
		gdbm? ( >=sys-libs/gdbm-1.8.0 )
		gmp? ( >=dev-libs/gmp-4.1.2 )
		iconv? ( virtual/libiconv )
		imap? (
			virtual/imap-c-client[ssl=]
			virtual/imap-c-client[kolab=]
		)
		intl? ( dev-libs/icu )
		iodbc? ( dev-db/libiodbc >=dev-db/unixODBC-1.8.13 )
		kerberos? ( virtual/krb5 )
		kolab? ( >=net-libs/c-client-2004g-r1 )
		ldap? ( !oci8? ( >=net-nds/openldap-1.2.11 ) )
		ldap-sasl? ( !oci8? ( dev-libs/cyrus-sasl >=net-nds/openldap-1.2.11 ) )
		libedit? ( || ( sys-freebsd/freebsd-lib dev-libs/libedit ) )
		mbstring? ( dev-libs/oniguruma )
		mcve? ( >=dev-libs/openssl-0.9.7 )
		mssql? ( dev-db/freetds )
		!mysqlnd? (
			mysql? ( virtual/mysql )
			mysqli? ( >=virtual/mysql-4.1 )
		)
		nls? ( sys-devel/gettext )
		oci8-instant-client? ( dev-db/oracle-instantclient-basic )
		odbc? ( >=dev-db/unixODBC-1.8.13 )
		phar? ( dev-php/PEAR-PHP_Archive )
		postgres? (
			|| (
				>=dev-db/postgresql-base-7.1[threads=]
				(
					|| (
						<dev-db/libpq-8
						>=dev-db/libpq-8[threads=]
					)
				)
			)
		)
		qdbm? ( dev-db/qdbm )
		readline? ( sys-libs/readline )
		recode? ( app-text/recode )
		sapdb? ( >=dev-db/unixODBC-1.8.13 )
		sharedmem? ( dev-libs/mm )
		simplexml? ( >=dev-libs/libxml2-2.6.8 )
		snmp? ( >=net-analyzer/net-snmp-5.2 )
		soap? ( >=dev-libs/libxml2-2.6.8 )
		solid? ( >=dev-db/unixODBC-1.8.13 )
		spell? ( >=app-text/aspell-0.50 )
		sqlite? ( =dev-db/sqlite-2* pdo? ( =dev-db/sqlite-3* ) )
		sqlite3? ( =dev-db/sqlite-3* )
		ssl? ( >=dev-libs/openssl-0.9.7 )
		tidy? ( app-text/htmltidy )
		truetype? (
			=media-libs/freetype-2*
			>=media-libs/t1lib-5.0.0
			!gd? ( !gd-external? (
				>=media-libs/jpeg-6b media-libs/libpng sys-libs/zlib ) )
		)
		unicode? ( dev-libs/oniguruma )
		wddx? ( >=dev-libs/libxml2-2.6.8 )
		xml? ( >=dev-libs/libxml2-2.6.8 )
		xmlrpc? ( >=dev-libs/libxml2-2.6.8 virtual/libiconv )
		xmlreader? ( >=dev-libs/libxml2-2.6.8 )
		xmlwriter? ( >=dev-libs/libxml2-2.6.8 )
		xpm? (
			x11-libs/libXpm
			>=media-libs/jpeg-6b
			media-libs/libpng sys-libs/zlib
		)
		xsl? ( dev-libs/libxslt >=dev-libs/libxml2-2.6.8 )
		zip? ( sys-libs/zlib )
		zlib? ( sys-libs/zlib )
		virtual/mta
	"

	local php="=${CATEGORY}/${PF}"
	RDEPEND="${DEPEND}
		truetype? ( || ( $php[gd] $php[gd-external] ) )
		cjk? ( || ( $php[gd] $php[gd-external] ) )
		exif? ( || ( $php[gd] $php[gd-external] ) )

		xpm? ( $php[gd] )
		gd? ( $php[zlib,-gd-external] )
		gd-external? ( $php[-gd] )
		fpm? ( $php[xml] )
		simplexml? ( $php[xml] )
		soap? ( $php[xml] )
		wddx? ( $php[xml] )
		xmlrpc? ( || ( $php[xml] $php[iconv] ) )
		xmlreader? ( $php[xml] )
		xsl? ( $php[xml] )
		ldap-sasl? ( $php[ldap,-oci8] )
		mcve? ( $php[ssl] )
		suhosin? ( $php[unicode] )
		adabas? ( $php[odbc] )
		birdstep? ( $php[odbc] )
		dbmaker? ( $php[odbc] )
		empress-bcs? ( $php[empress] )
		empress? ( $php[odbc] )
		esoob? ( $php[odbc] )
		db2? ( $php[odbc] )
		iodbc? ( $php[odbc] )
		sapdb? ( $php[odbc] )
		solid? ( $php[odbc] )
		kolab? ( $php[imap] )
		phar? ( $php[hash] )
		mysqlnd? ( || (
			$php[mysql]
			$php[mysqli]
			$php[pdo]
		) )

		oci8? ( $php[-oci8-instant-client,-ldap-sasl] )
		oci8-instant-client? ( $php[-oci8] )

		qdbm? ( $php[-gdbm] )
		readline? ( $php[-libedit] )
		recode? ( $php[-imap,-mysql,-mysqli] )
		firebird? ( $php[-interbase] )
		sharedmem? ( $php[-threads] )

		!cli? ( !cgi? ( !apache2? ( !embed? ( !fpm? ( $php[cli] ) ) ) ) )

		enchant? ( !dev-php${PHP_MV}/pecl-enchant )
		fileinfo? ( !dev-php${PHP_MV}/pecl-fileinfo )
		filter? ( !dev-php${PHP_MV}/pecl-filter )
		json? ( !dev-php${PHP_MV}/pecl-json )
		mcve? ( dev-php${PHP_MV}/pecl-mcve )
		phar? ( !dev-php${PHP_MV}/pecl-phar )
		yaz? ( dev-php${PHP_MV}/pecl-yaz )
		zip? ( !dev-php${PHP_MV}/pecl-zip )"

	# those are only needed at compile-time
	DEPEND="${DEPEND}
		>=sys-devel/m4-1.4.3
		>=sys-devel/libtool-1.5.18"

	# They are in PDEPEND because we need PHP installed first!
	PDEPEND="doc? ( app-doc/php-docs )
		suhosin? ( dev-php${PHP_MV}/suhosin )"

	# Portage doesn't support setting PROVIDE based on the USE flags that
	# have been enabled, so we have to PROVIDE everything for now and hope
	# for the best
	PROVIDE="virtual/php virtual/httpd-php"

	SLOT="${PHP_MV}"
	S="${WORKDIR}/${PHP_P}"

	PHP_INI_FILE="php.ini"
	PHP_INI_UPSTREAM="php.ini-production"

	want_apache
}
